buildscript {
    ext.kotlin_version = '1.2.60'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'org.jetbrains.intellij' version "0.3.5"
}

repositories {
    mavenCentral()
}

version = "${version}.${buildNumber}"

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'kotlin'

intellij {
    version ideaVersion
    plugins 'tasks', 'git4idea'
    pluginName 'intellij-gitee'

    downloadSources Boolean.valueOf(sources)
    sameSinceUntilBuild Boolean.valueOf(isEAP)

    publishPlugin {
        username publishUser
        password publishPassword
    }
}

allprojects {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = '1.8'
        }
    }

    def compilationPackages = ['org/intellij/gitee/build/**']

    test {
        useJUnit {
            exclude compilationPackages
        }
        testLogging {
            exceptionFormat = 'full'
        }
    }

    task testCompilation(type: Test, group: 'Verification', dependsOn: [classes, testClasses]) {
        useJUnit {
            include compilationPackages
        }
        testLogging {
            exceptionFormat = 'full'
        }
    }
}
